#!/usr/bin/env fish

# Parse command line args
set argc (count $argv)
if test "$argc" = 3
	set instance_file "$argv[1]"
	set timeout "$argv[2]"
	set num_workers "$argv[3]"
	set solver_args ""
else
	if test "$argc" = 4
		set instance_file "$argv[1]"
		set timeout "$argv[2]"
		set num_workers "$argv[3]"
		set solver_args "$argv[4]"
	else
		echo "Usage: perf_test <instance_file> <timeout> <num_workers> [solver_args]"
		exit 1
	end
end

# Get git revision and check for unstaged changes
git update-index --refresh > /dev/null
git diff-index --quiet HEAD
if test $status -ne 0
	echo "You have unstaged changes!"
	exit 2
end

set revision (git rev-parse --short HEAD)
set start_time (date -Isecond | tr ":" "-")

echo "Starting perf run with $num_workers threads and a timeout of $timeout minutes."
echo "Revision: $revision, date: $start_time"
echo "Instance file: $instance_file"
echo "Solver args: $solver_args"

# Store run info in summary file
set out_dir "data/runs/perf/"$revision"_$start_time"
mkdir -p "$out_dir"

echo "Executed with $num_workers threads and $timeout minute timeout." > "$out_dir/summary"
echo "Revision $revision and start time $start_time" >> "$out_dir/summary"
echo "Instance file: $instance_file" >> "$out_dir/summary"
echo "Solver args: $solver_args" >> "$out_dir/summary"
echo "Instances:" >> "$out_dir/summary"
cat "$instance_file" >> "$out_dir/summary"

rm runner_out/*

# Build and run
cargo build --release

if test -n "$solver_args"
	cargo run --release --bin runner -- --num-workers $num_workers --timeout $timeout --solver-args="$solver_args" runner_out (cat "$instance_file") &| tee runner_out/summary
else
	cargo run --release --bin runner -- --num-workers $num_workers --timeout $timeout runner_out (cat "$instance_file") &| tee runner_out/summary
end

# Store and output timing
set total_time (grep "Done. " "runner_out/summary" | sed -r 's/.* in ([0-9\.]+) seconds.*/\1/g')
echo "Took $total_time seconds."
echo "Took $total_time seconds." >> "$out_dir/summary"

# Copy output to storage
echo "Storing run results at $out_dir"
cp -r runner_out "$out_dir/"
cat "$out_dir/runner_out/summary" >> "$out_dir/summary"

echo "Perf run completed."

echo -n (date +%d-%m-%Y)
echo -n " - $revision: \""
echo -n (git log -1 --pretty=%s)
echo "\" ($solver_args)"
echo "	$total_time seconds, $out_dir"
